#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****


########################################################
#
# TI-Process: 2 IMPORT_CUBE_config.tm1s
#
# Loads configuration data from tm1s.cfg to cube config
#
# @author Marius Zappel GMC2
# @version 1.1
# @since 2015-05-07
# @TIscope Prolog, Metadata, Data, Epilog
# @variable vs_V1 As String
# @variable vs_V2 As String
# @param nothing
# @return nothing
# @exception nothing
# @update 2015-05-07
# @updatewho Marius Zappel, GMC2
# @updatewhat Initial setup
# @updateversion 1.0
# @update 2024-02-20
# @updatewho Grischa Rehmer, GMC2
# @updatewhat adaption to latest CFG file
# @updateversion 1.1
#
########################################################


########################################################
# PROGRAM CHART
########################################################
# 01 PROLOG: SET VARIABLES
# 02 PROLOG: SET DATASOURCE
# 03 METADATA: CHECK AND CREATE MISSING ELEMENTS
# 04 DATA: IMPORT DATA INTO CUBE
# 05 EPILOG: ERROR REPORT
########################################################


########################################################
# 01 SET VARIABLES
########################################################
cs_CubeTgt = 'config';
cs_DimItem = 'config_item';
cs_DimMeasure = 'config_measure';
cs_DimElemMeasureSetting = 'SETTING';
cs_DimElemMeasureActive = 'ACTIVE';
cs_DimElemItemUnassigned = 'UNASSIGNED';

cs_AttrIsCustom = 'Is_Custom';

s_Error = '';
cs_Concat = CHAR(10);
########################################################


########################################################
# 02 SET DATASOURCE
########################################################
DataSourceType = 'CHARACTERDELIMITED';
DatasourceNameForServer = '.\..\config\tm1s.cfg';
DatasourceNameForClient = '.\..\config\tm1s.cfg';
DatasourceASCIIDelimiter = '=';
########################################################
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****


########################################################
# 03 CHECK AND CREATE MISSING ELEMENTS
########################################################
If(SUBST(vs_V1, 1, 1) @= '#');
   vs_V1 = SUBST(vs_V1, 2, LONG(vs_V1));
EndIf;

#[TM1S] and a comment (starting with ###) should be skipped
If(SUBST(vs_V1, 1, 2) @= '##' % vs_V1 @= '[TM1S]');
   ItemSkip;
EndIf;

If(DIMIX(cs_DimItem, vs_V1) = 0);
   DimensionElementInsert(cs_DimItem, '', cs_DimElemItemUnassigned, 'C');
   DimensionElementInsert(cs_DimItem, '', vs_V1, 'N');
   DimensionElementComponentAdd(cs_DimItem, cs_DimElemItemUnassigned, vs_V1, 1);
EndIf;
########################################################
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****


########################################################
# 05 IMPORT DATA INTO CUBE
########################################################
n_IsActive = 1;
If(SUBST(vs_V1, 1, 1) @= '#');
   vs_V1 = SUBST(vs_V1, 2, LONG(vs_V1));
   n_IsActive = 0;
EndIf;

#[TM1S] and a comment (starting with ###) should be skipped
If(SUBST(vs_V1, 1, 2) @= '##' % vs_V1 @= '[TM1S]');
   ItemSkip;
EndIf;

If(ATTRN(cs_DimItem, vs_V1, cs_AttrIsCustom) <> 0);
   n_IsActive = 1;
EndIf;


CellPutS(vs_V2, cs_CubeTgt, vs_V1, cs_DimElemMeasureSetting);
CellPutN(n_IsActive, cs_CubeTgt, vs_V1, cs_DimElemMeasureActive);
########################################################
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****


########################################################
# 07 ERROR REPORT
########################################################
If(s_Error @<> '');
   s_Error = s_Error | cs_Concat | 'Process aborted.';
   ItemReject(s_Error);
EndIf;
########################################################
#endregion